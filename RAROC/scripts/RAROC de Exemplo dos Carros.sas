/*--------------------------------------------------------------------------*/
/*---           RAROC DA CARTEIRA DE FINANCIAMENTO DE VEÍCULOS           ---*/
/*--------------------------------------------------------------------------*/

OPTIONS COMPRESS=YES;
PROC SQL;
CREATE TABLE HISTORICO_MES AS 
SELECT CNPJ14, 
       EXTRACT(MONTH FROM DATA_CONTRATO) AS MES,
       AVG(VALOR_CONTRATO)      AS AVG_VR_TRN ,
       PERCENTILE_CONT(0.25) WITHIN GROUP (ORDER BY VALOR_CONTRATO) AS P25_VR_TRN ,
       PERCENTILE_CONT(0.75) WITHIN GROUP (ORDER BY VALOR_CONTRATO) AS P75_VR_TRN 

      FROM BNDES
	  WHERE DH_TRN BETWEEN '2020-01-01' AND '2020-03-31'
	  AND NR_CPR_CNA IN (3001) AND CD_TRN IN (240)
      GROUP BY CNPJ14, MES
	  ORDER BY CNPJ14, MES ;
QUIT;

/*--------------------------------------------------------------------------*/
/*---           RAROC DA CARTEIRA DE FINANCIAMENTO DE VEÍCULOS           ---*/
/*--------------------------------------------------------------------------*/



PROC SQL;
CREATE TABLE RAROC_VEICULOS_CPF AS
SELECT DISTINCT A.CNPJ14,CD_CON,DS_TPO_PSS,DT_MVM_DWM,QT_DD_ATS_PRL,QT_PRL,
          RISCO,PC_TX_JRO,B.PD,CAT_PRAZO,
          MAX(VR_SLD_DVD) AS VR_SLD_DVD,
		  MAX((PC_TX_JRO/100)*VR_SLD_DVD) as NUM_PC_TX_JRO,
          CASE WHEN AVG(b.PD)<=0.22 THEN 'AA'
		       WHEN AVG(b.PD)<=0.70 THEN 'A'
		       WHEN AVG(b.PD)<=1.03 THEN 'B'
			   WHEN AVG(b.PD)<=2.07 THEN 'C'
			   WHEN AVG(b.PD)<=5.46 THEN 'D'
			   WHEN AVG(b.PD)<=20   THEN 'E'
			   WHEN AVG(b.PD)<=60   THEN 'F'
			   WHEN AVG(b.PD)<=85   THEN 'G'
			   WHEN AVG(b.PD)<=100  THEN 'H'
			   ELSE '' END AS RISCO_BC ,
		  CASE WHEN QT_DD_ATS_PRL>90 THEN VR_SLD_DVD ELSE 0 END AS VR_SLD_DVD90

   FROM CONNECTION TO con1 
      ( SELECT DISTINCT CNPJ14,DS_TPO_PSS,CNAE,VR_LBR,QT_DD_ATS_PRL,
                        DT_MVM_DWM,PC_TX_JRO,QT_PRL,VR_SLD_DVD,CD_CON,
						MAX(DT_MVM_DWM) OVER (PARTITION BY CNPJ14,CNAE) AS DATA_MAX ,
			   CASE WHEN QT_PRL <= 6 THEN 'Ate 6 meses' 
			        WHEN QT_PRL <=12 THEN 'Entre 07 e 12 meses'
                    WHEN QT_PRL <=18 THEN 'Entre 13 e 18 meses'
			        WHEN QT_PRL <=24 THEN 'Entre 19 e 24 meses'
			        WHEN QT_PRL <=30 THEN 'Entre 25 e 30 meses'
			        WHEN QT_PRL <=36 THEN 'Entre 31 e 36 meses'
			        WHEN QT_PRL <=42 THEN 'Entre 37 e 42 meses'
			        WHEN QT_PRL <=48 THEN 'Entre 43 e 48 meses'
			        WHEN QT_PRL <=54 THEN 'Entre 49 e 54 meses'
                    WHEN QT_PRL <=60 THEN 'Entre 55 e 60 meses'
                    WHEN QT_PRL > 60 THEN 'Mais que 60 meses'
                    ELSE '' END as CAT_PRAZO 
        FROM BNDES
        WHERE DS_MDL_BACEN='FINANCIAMENTOS' 
              AND DS_SUB_MDL_BACEN='AQUISIÇÃO DE BENS – VEÍCULOS AUTOMOTORES' 
              AND CD_MDL_BACEN=4 
              AND CD_SUB_MDL_BACEN = 1
              AND DT_MVM_DWM > '2019-01-01' 
			  AND DT_LBR> '2017-01-01' 
              AND PC_TX_JRO>0 
              AND IC_MVM = 1 
              AND CD_NVL_RCO NOT IN ('HH')
			  AND DS_TPO_PSS='PF'
        ORDER BY CNPJ14
        ) AS A
		LEFT JOIN PLD.PD_COP_MARCO19_23_04 AS B ON (A.CNPJ14=B.CNPJ14 )
		WHERE B.PD NE . AND A.DT_MVM_DWM = A.DATA_MAX
        GROUP BY A.CNPJ14,A.CNAE,DS_TPO_PSS,VR_LBR,QT_DD_ATS_PRL,
                 DT_MVM_DWM,QT_PRL,Risco,PC_TX_JRO,B.PD,CAT_PRAZO,CD_CON,a.CNAE
        ORDER BY A.CNPJ14,A.CNAE ;
QUIT;



PROC SQL;
   CREATE TABLE COV_VAR_RAROC as
   SELECT CNAE,
		  CASE WHEN RISCO_BC='AA' THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_AA,
		  CASE WHEN RISCO_BC='A'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_A,
		  CASE WHEN RISCO_BC='B'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_B,
		  CASE WHEN RISCO_BC='C'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_C,
          CASE WHEN RISCO_BC='D'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_D,
          CASE WHEN RISCO_BC='E'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_E,
          CASE WHEN RISCO_BC='F'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_F,
          CASE WHEN RISCO_BC='G'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_G,
          CASE WHEN RISCO_BC='H'  THEN MAX(VR_SLD_DVD) ELSE 0 END AS SLD_DVD_H
   FROM RAROC_VEICULOS_CPF 
   GROUP BY CNAE
   ORDER BY CNAE;

   CREATE TABLE COV_VAR_RAROC as
   SELECT CNAE,
		  SUM(SLD_DVD_AA) AS SLD_DVD_AA,
		  SUM(SLD_DVD_A)  AS SLD_DVD_A ,
		  SUM(SLD_DVD_B)  AS SLD_DVD_B ,
		  SUM(SLD_DVD_C)  AS SLD_DVD_C ,
          SUM(SLD_DVD_D)  AS SLD_DVD_D ,
          SUM(SLD_DVD_E)  AS SLD_DVD_E ,
          SUM(SLD_DVD_F)  AS SLD_DVD_F ,
          SUM(SLD_DVD_G)  AS SLD_DVD_G ,
          SUM(SLD_DVD_H)  AS SLD_DVD_H
   FROM COV_VAR_RAROC 
   GROUP BY CNAE
   ORDER BY CNAE;
QUIT;



PROC CORR DATA=COV_VAR_RAROC NOPRINT OUT=CORREL(WHERE=(_TYPE_='CORR'));
VAR SLD_DVD_AA SLD_DVD_A SLD_DVD_B SLD_DVD_C SLD_DVD_D SLD_DVD_E SLD_DVD_F SLD_DVD_G SLD_DVD_H;
RUN;




/*PERDA ESPERADA E DESVIO-PADRÃO DA PERDA ESPERADA*/
PROC MEANS DATA=RAROC_VEICULOS_CPF NOPRINT NWAY;
CLASS RISCO_BC;
VAR PD;
WEIGHT VR_SLD_DVD;
OUTPUT OUT=SAIDA(DROP=_FREQ_ _TYPE_) MEAN(PD)=PD_MEDIA STD(PD)=PD_STD;
RUN;


PROC SQL;
CREATE TABLE SAIDA AS 
SELECT * , CASE WHEN PD_STD=0 THEN 5*PD_MEDIA ELSE PD_STD END AS DESVIO_PERDA 
FROM SAIDA ;
QUIT;



/*QUANTIL NÃO PARAMÉTRICO DOS SALDOS POR TIPO DE PESSOA E RISCO*/
PROC SORT DATA=RAROC_VEICULOS_CPF SORTSIZE=MAX;BY CAT_PRAZO RISCO_BC;RUN;
PROC STANDARD
   DATA=RAROC_VEICULOS_CPF(KEEP=CNPJ14 VR_SLD_DVD CAT_PRAZO RISCO_BC)
   MEAN=0 STD=1 REPLACE NOPRINT OUT=zscore;
   BY CAT_PRAZO RISCO_BC;
   VAR VR_SLD_DVD;
RUN;


PROC MEANS DATA=zscore NOPRINT NWAY;
CLASS RISCO_BC;
VAR VR_SLD_DVD;
OUTPUT OUT=QUANTIL(DROP=_FREQ_ _TYPE_) P95(VR_SLD_DVD)=QUANTIL95 P99(VR_SLD_DVD)=QUANTIL99;
RUN;



OPTIONS COMPRESS=YES;
PROC SQL;
CREATE TABLE FINAN_CARROS AS 
  SELECT A.RISCO_BC AS RATING,
		 COUNT(*) AS QTD_OPERACOES,
         CAT_PRAZO,
		 SUM(VR_SLD_DVD) AS VALOR_OPERACOES,
		 SUM(NUM_PC_TX_JRO)/SUM(VR_SLD_DVD) AS TAXA_JUROS,
         QT_PRL AS PRAZO,
		 SUM(VR_SLD_DVD)*((1+CALCULATED TAXA_JUROS)**QT_PRL) AS VALOR_FINAL,
		 SUM(VR_SLD_DVD)*(((1+CALCULATED TAXA_JUROS)**QT_PRL)*CALCULATED TAXA_JUROS)/(((1+CALCULATED TAXA_JUROS)**QT_PRL)-1) AS PRESTACAO_PRICE,
		 SUM(VR_SLD_DVD)*(1+0.07) AS CUSTO_CAPTACAO,
		 QT_PRL*CALCULATED PRESTACAO_PRICE AS MONTANTE_NOMINAL,
		 10*COUNT(*) AS TAXAS_COMISSOES,
		 0.03*CALCULATED MONTANTE_NOMINAL AS CUSTOS_OPERACIONAIS,
		 MEAN(PD/100) AS PERDA_ESPERADA,
		 DESVIO_PERDA/100 AS DESVIO_PERDA,
		 (MEAN(PD)/100)*CALCULATED MONTANTE_NOMINAL AS VALOR_PERDA_ESPERADA,
		 CALCULATED MONTANTE_NOMINAL-CALCULATED CUSTO_CAPTACAO AS SPREAD,
		 CALCULATED SPREAD+CALCULATED TAXAS_COMISSOES-CALCULATED CUSTOS_OPERACIONAIS-CALCULATED VALOR_PERDA_ESPERADA  AS LUCRO_AJUSTADO,
		 C.QUANTIL95 AS NIVEL_CONFIANCA,
		 C.QUANTIL95*(DESVIO_PERDA/100)*SUM(VR_SLD_DVD) AS PERDA_INESPERADA

      FROM RAROC_VEICULOS_CPF AS A
	  LEFT outer join SAIDA   AS B ON A.RISCO_BC=B.RISCO_BC 
	  LEFT outer join QUANTIL AS C ON A.RISCO_BC=C.RISCO_BC  
      GROUP BY QT_PRL,A.RISCO_BC,CAT_PRAZO,DESVIO_PERDA,C.QUANTIL99
	  ORDER BY A.RISCO_BC;
QUIT;



PROC SQL;
CREATE TABLE FINAN_CARROS2 AS 
  SELECT RATING,
         SUM(LUCRO_AJUSTADO) AS LUCRO_AJUSTADO,
         SUM(PERDA_INESPERADA) AS PERDA_INESPERADA
  FROM FINAN_CARROS
  GROUP BY RATING
  ORDER BY RATING;
QUIT;


/*****************************************************/
/*****************************************************/
/***                CALCULANDO VaR                 ***/
/*****************************************************/
/*****************************************************/

DATA MATRIX;
INPUT RATING $ SIG_AA SIG_A SIG_B SIG_C SIG_D SIG_E SIG_F SIG_G SIG_H;
CARDS;
AA 1.00 0.20 0.20 0.30 0.40 0.10 0.10 0.10 0.10
A  0.20 1.00 0.20 0.20 0.30 0.40 0.10 0.10 0.10
B  0.20 0.20 1.00 0.30 0.30 0.10 0.10 0.10 0.10
C  0.30 0.30 0.30 1.00 0.20 0.10 0.10 0.10 0.10
D  0.40 0.40 0.30 0.30 1.00 0.10 0.10 0.10 0.10
E  0.10 0.10 0.10 0.10 0.10 1.00 0.10 0.10 0.10
F  0.10 0.10 0.10 0.10 0.10 0.10 1.00 0.10 0.10
G  0.10 0.10 0.10 0.10 0.10 0.10 0.10 1.00 0.10
H  0.10 0.10 0.10 0.10 0.10 0.10 0.10 0.10 1.00
;
RUN;

DATA CORREL(DROP=_NAME_);
RETAIN RATING;
SET CORREL(DROP=_TYPE_);
     IF _NAME_='SLD_DVD_AA' THEN RATING='AA';
ELSE IF _NAME_='SLD_DVD_A'  THEN RATING='A';
ELSE IF _NAME_='SLD_DVD_B'  THEN RATING='B';
ELSE IF _NAME_='SLD_DVD_C'  THEN RATING='C';
ELSE IF _NAME_='SLD_DVD_D'  THEN RATING='D';
ELSE IF _NAME_='SLD_DVD_E'  THEN RATING='E';
ELSE IF _NAME_='SLD_DVD_F'  THEN RATING='F';
ELSE IF _NAME_='SLD_DVD_G'  THEN RATING='G';
ELSE IF _NAME_='SLD_DVD_H'  THEN RATING='H';
RENAME SLD_DVD_AA = SIG_AA 
       SLD_DVD_A  = SIG_A 
       SLD_DVD_B  = SIG_B 
       SLD_DVD_C  = SIG_C 
       SLD_DVD_D  = SIG_D
       SLD_DVD_E  = SIG_E
       SLD_DVD_F  = SIG_F
       SLD_DVD_G  = SIG_G
       SLD_DVD_H  = SIG_H;
RUN;


DATA _NULL_; SET CORREL;
CALL SYMPUT('T'||LEFT(_N_),RATING);
RUN;

/*%PUT &T1. &T2. &T3. &T4. &T5. &T6. &T7. &T8. &T9.;*/


%MACRO MULT;
PROC SQL NOPRINT;
SELECT COUNT(*) INTO:N FROM FINAN_CARROS2;
QUIT;
DATA FIM; SET FINAN_CARROS2;RUN;
PROC SORT DATA=CORREL;BY RATING;RUN;
PROC SORT DATA=FIM   ;BY RATING;RUN;

%DO I = 1 %TO &N.;
  PROC SQL NOPRINT;
  SELECT PERDA_INESPERADA INTO:VAL FROM FINAN_CARROS2 WHERE RATING = "&&T&I.";
  QUIT;
  DATA FIM(compress=yes);MERGE FIM CORREL;
  BY RATING;
  VAR_&&T&I. = &VAL.*PERDA_INESPERADA;
  RUN;
  DATA FIM;SET FIM;
  VaR_&&T&I. = VAR_&&T&I.*SIG_&&T&I.;
  RUN;
%END;
%MEND MULT;
%MULT;


PROC SQL NOPRINT;
CREATE TABLE RAROC AS  
SELECT SUM(VaR_AA)+SUM(VaR_A)+SUM(VaR_B)+SUM(VaR_C)+SUM(VaR_D)+SUM(VaR_E)+SUM(VaR_F)+SUM(VaR_G)+SUM(VaR_H) AS VaR,
       SUM(LUCRO_AJUSTADO) AS LUCRO_AJUSTADO,
	   SUM(LUCRO_AJUSTADO)/SQRT(CALCULATED VaR) AS RAROC
FROM FIM ;
QUIT;



/*------------------------------------------------------------*/
/*------------------------------------------------------------*/
/*---          RAROC E TAXA JUROS SIMULADO VaR             ---*/
/*------------------------------------------------------------*/
/*------------------------------------------------------------*/


DATA PD;
INPUT RATING $ PD QPROV99;
DO TAXA_JUROS=0.005 TO 0.025 BY 0.005;
DO PRAZO=12 TO 60 BY 12;
OUTPUT;
END;
END;
CARDS;
AA 0.0022 0.03
A  0.0070 0.10
B  0.0103 0.30
C  0.0546 0.50
D  0.2000 0.50
E  0.4000 0.70
F  0.6000 1.00
G  0.8500 1.20
H  1.0000 1.20
;
RUN;



DATA RAROC;SET PD;
VALOR_OPERACOES   = 35000;
GARANTIA           = 0.38;
VALOR_FINAL        = VALOR_OPERACOES*((1+TAXA_JUROS)**PRAZO);
PRESTACAO_FORMULA = VALOR_OPERACOES*((((1+TAXA_JUROS)**PRAZO)*TAXA_JUROS)/(((1+TAXA_JUROS)**PRAZO)-1));
PRESTACAO_PRICE    = FINANCE('pmt',TAXA_JUROS,PRAZO,-VALOR_OPERACOES,0);
SELIC               =   0.00525383 ;
SELIC_PRAZO        = ((1+SELIC)**PRAZO)-1;
CUSTO_CAPTACAO     = VALOR_OPERACOES*(1+SELIC_PRAZO);
MONTANTE_NOMINAL   = PRAZO*PRESTACAO_PRICE;
CUSTOS_OPER        = 0.02*MONTANTE_NOMINAL;
PD                  = PD*(1+0.27)**(PRAZO*0.27);
PERDA_ESPERADA    = PD*(1-GARANTIA)*VALOR_OPERACOES;
SPREAD             = MONTANTE_NOMINAL-CUSTO_CAPTACAO;
LUCRO_AJUSTADO    = SPREAD-CUSTOS_OPER-PERDA_ESPERADA;
VAR                 = PERDA_ESPERADA+(1-GARANTIA)*(QPROV99*VALOR_OPERACOES);
RAROC              = LUCRO_AJUSTADO / VAR;
RUN;

